@AbapCatalog.sqlViewName: 'ZTRAVEL_U'
@AbapCatalog.compiler.compareFilter: true
@AbapCatalog.preserveKey: true
@AccessControl.authorizationCheck: #NOT_REQUIRED
@EndUserText.label: 'Travel CDS - Unmanaged'
define root view Z_I_TRAVEL_U
  as select from /dmo/travel as Travel
  composition [0..*] of Z_I_BOOKING_U   as _Booking
  association [0..1] to /DMO/I_Agency   as _Agency   on $projection.AgencyID = _Agency.AgencyID
  association [0..1] to /DMO/I_Customer as _Customer on $projection.CustomerID = _Customer.CustomerID
  association [0..1] to I_Currency      as _Currency on $projection.CurrencyCode = _Currency.Currency
{
      //Travel
  key Travel.travel_id     as TravelID,
      Travel.agency_id     as AgencyID,
      Travel.customer_id   as CustomerID,
      Travel.begin_date    as BeginDate,
      Travel.end_date      as EndDate,
      @Semantics.amount.currencyCode: 'CurrencyCode'
      Travel.booking_fee   as BookingFee,
      @Semantics.amount.currencyCode: 'CurrencyCode'
      Travel.total_price   as TotalPrice,
      @Semantics.currencyCode: true      
      Travel.currency_code as CurrencyCode,
      Travel.description   as Memo,
      Travel.status        as Status,
      Travel.lastchangedat as LastChangedAt,

      _Booking,
      _Agency,
      _Customer,
      _Currency // Make association public
}
